#!/usr/bin/env zsh

fpath+="${HOME}/.local/share/zsh/site-functions/"

# Generate any missing completion scripts
_commands=(
	docker
	flux
	grant
	grype
	helm
	k9s
	kube-linter
	kubectl
	kubescape
	pnpm
	syft
)

for _command in ${_commands[@]}; do
	_completion_path="${HOME}/.local/share/zsh/site-functions/_$_command";

	# If the command is not installed, skip it
	if ! command -v "$_command" >& /dev/null; then
		continue
	fi

	# If the completion file does not exist, or the binary is newer, generate completion
	if [[ ! -f "$_completion_path" || $(command -v "$_command") -nt "$_completion_path" ]]; then
		"$_command" completion zsh > "$_completion_path"
	fi
done

# Load completions
autoload -U compinit
compinit

# Configure completion behavior
zstyle ':completion:*' menu select
zstyle ':completion:*' matcher-list '' 'm:{a-zA-Z}={A-Za-z}'

